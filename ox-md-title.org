#+title: ox-md-title: Document titles for ox-md.el
#+author: Jeff Kreeftmeijer
#+date: 2023-03-10
#+options: toc:nil

Ox-md-title.el adds document titles to markdown files generated with ox-md and derivatives.
It consists of a single advice, which is added around the =org-md-template= function to prepend the document title:

#+name: advice
#+begin_src emacs-lisp
  (defgroup org-export-md-title nil
    "Options for org-md-title."
    :tag "Org Markdown Title"
    :group 'org-export
    :version "24.4"
    :package-version '(Org . "8.0"))

  (defcustom org-md-title nil
    "Non-nil means to include the title in the exported document."
    :group 'org-export-md-title
    :version "24.4"
    :package-version '(Org . "8.0")
    :type 'boolean)

  (defun org-md-title--advise-template (orig-fun &rest args)
    (concat
     (when org-md-title
       (let* ((info (nth 1 args))
	      (style (plist-get info :md-headline-style))
	      (title (org-export-data (plist-get info :title) info)))
	 (org-md--headline-title style 0 title nil)))
     (apply orig-fun args)))

  (defun org-md-title--advise-headline (args)
    (when org-md-title
      (setf (nth 1 args) (+ (nth 1 args) 1)))
    args)

  (defun org-md-title-add ()
    (advice-add 'org-md--headline-title :filter-args #'org-md-title--advise-headline)
    (advice-add 'org-md-template :around #'org-md-title--advise-template))

  (defun org-md-title-remove ()
    (advice-remove 'org-md--headline-title #'org-md-title--advise-headline)
    (advice-remove 'org-md-template #'org-md-title--advise-template))
#+end_src

#+headers: :tangle ox-md-title.el
#+headers: :noweb yes
#+headers: :exports none
#+begin_src emacs-lisp
  ;;; ox-md-title.el --- Document titles for ox-md.el

  ;;; Commentary:

  ;; ox-md-title.el adds document titles to markdown files generated
  ;; with ox-md and derivatives.

  ;;; Code:

  (require 'ox-md)

  <<advice>>

  (provide 'ox-md-title)

  ;;; ox-md-title.el ends here
#+end_src

* Usage

Call =org-md-title-add= to add ox-md-title's advice before exporting[fn:gfm], and call =org-md-title-remove= after:

#+begin_src emacs-lisp
  (use-package ox-gfm)
  (require 'ox-md-title)

  (let ((org-md-title t))
    (org-gfm-export-to-markdown))
#+end_src

[fn:gfm] This example uses [[https://github.com/larstvei/ox-gfm][ox-gfm]], which works because it's a backend derived from ox-md.
